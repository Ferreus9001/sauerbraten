// File:        gamespeed.cfg
// Description: A script intended for keybindings changing the game speed and spectator float speed
// Author:      FerreusRemedium
// Created:     2013-02-10
// Last Change: 2013-02-23

// uses fr.cfg library
exec fr.cfg

gamespeedtab = "10 20 35 50 75 100 150 200 300 500 1000"
floatspeedtab = "1 2 5 7 10 20 50 75 100 150 200 300 400 500 750 1000 1250 1500 2000 3000 5000 7500 10000"

// /floatspeed_increase N
// Increases the floatspeed to the value N steps after the current gamespeed in the list "floatspeedtab".
floatspeed_increase = [
    local newspeed
    newspeed = (fr_tab_reverseget $floatspeedtab $floatspeed $arg1)
    // floatspeed does not echo its value
    echo (format "^f7floatspeed is %1" $newspeed)
    floatspeed $newspeed
]

// global vars
_gsi_isheld = 0
_gsi_isrecent = 0
_gsi_lastevent = 0
_gsi_revertvalue = 100

// /gamespeed_increase N
// Sets the gamespeed to the value N steps after the current gamespeed in the list "gamespeedtab".
// N may be negative.
//
// When used as a binding, if the key is pressed for 500 milliseconds realtime,
// the speed is further increased by 2*N steps (resulting in 3*N steps increase).
// The speed is then reverted to its original value when the key is released.
// When pressing the key multiple times less than 500ms realtime apart before finally holding
// the key at least 500ms, the speed is reverted to its original value before pressing
// the key the first time.
// This behavior is disabled when the game is paused, because sleep does not work then.
gamespeed_increase = [
    if (! $_gsi_isrecent) [
        // save the original value
        _gsi_revertvalue = $gamespeed
    ]
    _gsi_isheld = 0
    _gsi_isrecent = 0
    _gsi_lastevent = (+ $_gsi_lastevent 1)

    gamespeed (fr_tab_reverseget $gamespeedtab $gamespeed $arg1)

    // holding

    // sleep doesn't work when paused
    if (! $paused) [
        _gsi_isrecent = 1
        // sleep 500 milliseconds realtime
        sleep (* 5 $gamespeed) [
            // check if this was issued by the most recent event
            if (= $_gsi_lastevent @_gsi_lastevent) [
                _gsi_isheld = 1
                gamespeed (fr_tab_reverseget $gamespeedtab $gamespeed (* 2 @@arg1))
            ]
            // must also be executed after release of the key
            if (<= $_gsi_lastevent @(+ $_gsi_lastevent 1)) [
                _gsi_isrecent = 0
            ]
        ]

        onrelease [
            _gsi_lastevent = (+ $_gsi_lastevent 1)
            if $_gsi_isheld [
                // released after key was held down
                _gsi_isheld = 0
                gamespeed $_gsi_revertvalue
            ]
        ]
    ]
]

// vim:set sw=4 ts=4 sts=0 ft=cube fdm=syntax et:
